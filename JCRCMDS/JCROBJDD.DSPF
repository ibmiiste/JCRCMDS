      *----------------------------------------------------------------
      * JCROBJDD - Expanded work with object descriptions - DSPF
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      INDARA
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA13
     A                                      CA14
     A                                      MOUBTN(*ULP CA13)
     A                                      MOUBTN(*URP CA14)
     A          R SBFDTA1                   SFL
     A            HIDRISFL       1A  H
     A            HIDNDSFL       1A  H
     A            HIDSORTCRT     7A  H
     A            HIDSORTLST     7A  H
     A            HIDOTYPE      10A  H
     A            HIDOLIB       10A  H
     A            AOPTIONSFL     1A  P
     A            ARISFL         1A  P
     A            ANDSFL         1A  P
     A            SBFOPTION      1Y 0B  7  2EDTCDE(4) DSPATR(&AOPTIONSFL)
     A            OBJNAME       10A  O  7  4DSPATR(&ARISFL)
     A            OBJTYPE        7A  O  7 15DSPATR(&ARISFL)
     A            OBJATTR        4A  O  7 23DSPATR(&ARISFL)
     A            OBJTEXT       19A  O  7 28DSPATR(&ARISFL)
     A            OBJSIZE        9Y 0O  7 48EDTCDE(4) DSPATR(&ARISFL)
     A            CREATEDATE     6Y 0O  7 58EDTCDE(Y) DSPATR(&ARISFL)
     A            LASTUSED       6Y 0O  7 67EDTCDE(Y) DSPATR(&ANDSFL)
     A            DAYSUSED       4Y 0O  7 76EDTCDE(3) DSPATR(&ARISFL)
      *----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1)
     A  *DS3                                SFLSIZ(0180)
     A  *DS4                                SFLSIZ(0180)
     A  *DS3                                SFLPAG(0015)
     A  *DS4                                SFLPAG(0015)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&CURRCD &CURFLD)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N32                                  SFLCLR
     A N34                                  SFLEND(*MORE)
     A            SFLRCDNBR      4S 0H      SFLRCDNBR(CURSOR)
     A            CURRCD        10A  H
     A            CURFLD        10A  H
     A            AOPTIONS       1A  P
     A            ASHOWUSR1      1A  P
     A            ASHOWUSR2      1A  P
     A            ANOTUSED       1A  P
     A                                  1  2'JCROBJD'
     A                                      COLOR(BLU)
     A                                  1 23'Expanded Work With Objects'
     A                                      DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2'Object'
     A            HEADEROBJ     10A  B  2  9DSPATR(HI)
     A                                  2 21'Lib'
     A            HEADERLIB     10A  B  2 25DSPATR(HI)
     A                                  2 37'Created by UserProfile:'
     A                                      DSPATR(&ASHOWUSR1)
     A            P_CRTUSR      10A  O  2 61DSPATR(&ASHOWUSR2)
     A                                  2 72SYSNAME
     A                                      COLOR(BLU)
     A                                  4  2'1=Wrkobj'
     A                                      DSPATR(&AOPTIONS)
     A                                  4 32'5=Dspobjd'
     A                                      DSPATR(&AOPTIONS)
     A                                  4 11'3=Dspobjlck'
     A                                      DSPATR(&AOPTIONS)
     A                                  4 23'4=DELETE'
     A                                      DSPATR(&AOPTIONS)
     A                                  4 51'7=CLRPFM'
     A                                      DSPATR(&AOPTIONS)
     A            DBUTILITY      8A  O  4 42DSPATR(&AOPTIONS)
     A                                  4 61'8=*ALLUSR'
     A                                      DSPATR(&AOPTIONS)
     A                                  4 71'9=WRKMBR'
     A                                      DSPATR(&AOPTIONS)
     A                                  3 63'No Last Used Date'
     A                                      DSPATR(&ANOTUSED)
     A                                      DSPATR(RI)
     A                                  5 68'Last'
     A                                      DSPATR(HI)
     A                                  5 74'Times'
     A                                      DSPATR(HI)
     A                                  6  2'Opt'
     A                                      DSPATR(HI)
     A                                  6  7'Object'
     A                                      DSPATR(HI)
     A                                  6 15'Type'
     A                                      DSPATR(HI)
     A                                  6 23'Attr'
     A                                      DSPATR(HI)
     A                                  6 28'Text'
     A                                      DSPATR(HI)
     A                                  6 50'Size(K)'
     A                                      DSPATR(HI)
     A                                  6 58'Created'
     A                                      DSPATR(HI)
     A                                  6 68'Used'
     A                                      DSPATR(HI)
     A                                  6 75'Used'
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R SFOOTER1                  OVERLAY BLINK
     A                                 23  2'F3=Exit' COLOR(BLU)
     A                                 23 11'F5=Refresh' COLOR(BLU)
     A                                 23 24'F13=Sort Ascend' COLOR(BLU)
     A                                 23 45'F14=Sort Descend' COLOR(BLU)
     A                                 23 69'F12=Cancel' COLOR(BLU)
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD(24)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PROGID                    SFLPGMQ(10)
