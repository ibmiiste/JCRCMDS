      *----------------------------------------------------------------
      * JCRUSERS - Search User Profiles
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(27 132 *DS4)
     A                                      INDARA
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA08
     A                                      CA12
     A                                      PRINT
      *----------------------------------------------
     A          R SBFDTA1                   SFL
     A            SFINITLIB     10   H
     A            SFOPT          1A  B  6  2
     A            SFUSRPRF      10A  O  6  5
     A            SFTEXT        39A  O  6 16
     A            SFSTATUS      10A  O  6 56
     A            SFINITPGM     10   O  6 67
     A            SFJOBD        10   O  6 78
     A            SFJOBDLIB     10   O  6 89
     A            SFOUTQ        10   O  6100
     A            SFOUTQLIB     10   O  6111
     A            SFLASTUSED    10A  O  6122
      *----------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1)
     A                                      SFLSIZ(1900)
     A                                      SFLPAG(0019)
     A                                      CHANGE(12)
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N31                                  SFLCLR
     A N34                                  SFLEND(*MORE)
     A                                  1  2'JCRUSERS'
     A                                      COLOR(BLU)
     A                                  1 23'Search User Profiles'
     A                                      DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTWRD('0  /  /  ')
     A                                      COLOR(BLU)
     A                                  2  5'PosTo'
     A                                      COLOR(BLU)
     A                                  2 16'(leading space = contains) '
     A                                      COLOR(BLU)
     A                                  2 72SYSNAME
     A                                      COLOR(BLU)
     A                                  3  2'2=ChgUsrPrf'
     A                                      COLOR(BLU)
     A                                  3 15'5=DspUsrPrf'
     A                                      COLOR(BLU)
     A                                  3 29'6=Email Address'
     A                                      COLOR(BLU)
     A                                  3 47'9=WrkUsrPrf'
     A                                      COLOR(BLU)
     A            SCUSER        10A  B  4  2CHANGE(12)
     A            SCTEXT        40A  B  4 16
     A            SCINIT        10A  B  4 67
     A            SCJOBD        10A  B  4 78
     A            SCOUTQ        10A  B  4100
     A                                  5  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  5'User Id   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 16'Text                              -
     A                                           '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 56'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 67'Init Pgm  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 78'Jodb                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5100'Outq                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5123'Last Used '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *----------------------------------------------
     A          R SFOOTER1
     A                                      OVERLAY
     A                                 26  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 26 34'F6=Print'
     A                                      COLOR(BLU)
     A                                 26 54'F8=Wrksplf'
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R EMAILWIN
     A                                      WINDOW(3 10 07 110 *NOMSGLIN)
     A                                      WDWTITLE((*TEXT 'Email Address') (*-
     A                                      COLOR WHT) (*DSPATR HI))
     A                                      OVERLAY
     A            WCUSRPRF      10   O  2  2
     A            WCTEXT        39   O  2 14
     A            WCADDRESS    100A  O  4  2DSPATR(HI)
     A                                  6  2'Enter=Proceed'
     A                                      COLOR(BLU)
      *----------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD(27)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PROGID                    SFLPGMQ(10)
